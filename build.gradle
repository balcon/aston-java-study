plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootPlugin}"
    id 'io.spring.dependency-management' version "${springDependencyManagmentPlugin}"
    id 'io.freefair.lombok' version "${lombokPlugin}"
    id 'jacoco'
}

group = 'com.github.balcon'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    // Persistence
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'

    // Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdoc}"

    // Monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Other
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "org.mapstruct:mapstruct:${mapstruct}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct}"
    implementation project(':service-logger-starter')

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
    }
    // Exclude packages for test coverage
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/github/balcon/venue/entity/*",
                    "com/github/balcon/venue/dto/*"
            ])
        }))
    }
}

jacoco {
    toolVersion = "0.8.11"
}

compileJava {
    options.compilerArgs = ['-Amapstruct.defaultComponentModel=spring']
}
